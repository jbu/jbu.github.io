<!DOCTYPE html>
<html lang="en">
  <head>
    <meta charset="utf-8">
    <link rel="stylesheet" href="https://latex.now.sh/style.css" />
    <script id="MathJax-script" async
      src="https://cdn.jsdelivr.net/npm/mathjax@3/es5/tex-mml-chtml.js"></script>
    <link rel="stylesheet" href="https://latex.now.sh/prism/prism.css">
    <script src="https://cdn.jsdelivr.net/npm/prismjs/prism.min.js"></script>
  </head>

  <body class="libertinus latex-dark-auto">

    <h1>
      Expanding Reducers
    </h1>
    <p class="author"><a href="../index.html">James Uther</a><br>2013-07-31</p>
    <p>
      When playing with a new bit of language, it can be helpful to restrict the
      problem space to an old, well understood algorithm. For me at least,
      learning one thing at a time is easier! For this post, It'll be prime
      sieves, and I'll be exploring clojure reducers.
    </p><p>
      A quick recap, the sieve of eratosthenes is a not-maximally-non-optimal
      way
      of finding primes. It's usually expressed as follows:

    </p><p>
      To find primes below n: generate a list of n integers greater than 1
      <pre>
      <code class="language-haskell">
while the list is not empty:
take the head of the list:
add it to the output
remove all numbers evenly divisible by it from the list
      </code></pre>
    </p>
    In clojure, something like:
    <pre>
      <code class="language-clojure">
(defn sieve
([n] (sieve [] (range 2 n)))
([primes xs]
  (if-let [prime (first xs)]
    (recur (conj primes prime)
          (remove #(zero? (mod % prime)) xs))
    primes)))

(sieve 10)
;= [2 3 5 7]
  </code></pre>
<p>
    Which is fine, but I'd like it lazy so I only pay for what I use, and I can
    use as much as I'm willing to pay for. Let's look at lazy sequences. Luckily
    for us, there is an example of exactly this on the lazy-seq documentation,
    which we slightly modify like so:

    <pre>
      <code class="language-clojure">
(defn lazy-sieve [s]
  (cons (first s)
        (lazy-seq
          (lazy-sieve (remove #(zero? (mod % (first s))) (rest s))))))
(defn primes []
  (lazy-seq (lazy-sieve (iterate inc 2))))

(take 5 (primes))
;= (2 3 5 7)
  </code></pre>
</p><p>
      So now we have a nice generic source of primes that grows only as we take
    more. But is there another way?
  </p><p>

    A few months ago Rich Hickey introduced reducers. By turning the concept of
    'reducing' inside out the new framework allows a parallel reduce (fold) in
    some circumstances. Which doesn't apply here. But let's see if we can build
    a different form of sieve using the new framework. First a quick overview
    (cribbing from the original blog post):
  </p><p>
<blockquote>
    Collections are now _reducible_, in that they implement a \(reduce\)
    protocol. \(Filter\), \(map\), etc are implemented as functions that
    can be applied by a reducible to itself to return another reducible, but
    lazily, and possibly in parallel. So in the example
    below we have a reducible (a vector), that maps inc to itself to return a
    reducible that is then wrapped with a filter on
    \(even?\) which returns a further reducible, that reduce then collects with
    \(+\).
  </blockquote>
    <pre>
      <code class="language-clojure">
(require '[clojure.core.reducers :as r])
; We'll be referring to r here and there â€“ just remember it's the
clojure.core.reducers namespace

(reduce + (r/filter even? (r/map inc [1 1 1 2])))
;= 6
  </code></pre>
    These are composable, so we can build 'recipes'.

    <pre>
      <code class="language-clojure">
;;red is a reducer awaiting a collection
(def red (comp (r/filter even?) (r/map inc)))
(reduce + (red [1 1 1 2]))
;= 6
  </code></pre>
    \(into\) uses reduce internally, so we can use it to build collections instead
    of reducing:
    <pre>
      <code class="language-clojure">
(into [] (r/filter even? (r/map inc [1 1 1 2])))
;= [2 2 2]
  </code></pre>
    So here's the core of 'reducer', which
    > Given a reducible collection, and a transformation function \(xf\), returns
    a reducible collection, where any supplied reducing \(fn\) will be
    transformed by \(xf\). \(xf\) is a function of reducing \(fn\) to reducing
    \(fn\).

    <pre>
      <code class="language-clojure">
(defn reducer ([coll xf]
  (reify clojure.core.protocols/CollReduce
    (coll-reduce [_ f1 init] (clojure.core.protocols/coll-reduce coll (xf f1)
                                                                  init))))
  </code></pre>

    And we can then use that to implement mapping as so:
    <pre>
      <code class="language-clojure">
(defn mapping [f]
(fn [f1] (fn [result input] (f1 result (f input)))))
  
(defn rmap [f coll] (reducer coll (mapping f)))
(reduce + 0 (rmap inc [1 2 3 4]))
;= 14
  </code></pre>
    Fine. So what about sieves? One thought is we could build up a list of
    composed filters, built as new primes are found (see the \(lazy-seq\) example
    above). But there's no obvious place to do the building, as applying the
    reducing functions is left to the reducible implementation. Another
    possibility is to introduce a new type of reducing function, the
    'progressive-filter', which keeps track of past finds and can filter against
    them.
    <pre>
      <code class="language-clojure">
(defn prog-filter [f] 
  (let [flt (atom [])] 
    (fn [f1] (fn [result input] 
                (if (not-any? #(f input %) @flt) 
                  (do (swap! flt conj input) 
                      (f1 result input)) 
                  result)))))

(defn progressive-filter [f coll]
  (reducer coll (prog-filter f)))
  </code></pre>
    And we then reduce with a filtering function that is a function of the
    current candidate and one of the list of found primes (see the <i>#(f input
    %)</i> bit above)

    <pre>
      <code class="language-clojure">
(into [] (progressive-filter #(zero? (mod %1 %2)) (range 2 10)))
;= [2 3 5 7]
  </code></pre>

    It's nicely lazy, so we can use iterate to generate integers, and take only
    a few (\(r/take\), as it's operating on a reducer):

    <pre>
      <code class="language-clojure">
(into [] (r/take 5 (progressive-filter #(zero? (mod %1 %2)) (iterate inc 2))))

;= [2 3 5 7 11]
  </code></pre>
    Or even
    <pre>
      <code class="language-clojure">
(def primes 
  (progressive-filter #(zero? (mod %1 %2)) (iterate inc 2)))

(into [] (r/take 5 primes))
;= [2 3 5 7 11]
  </code></pre>
    You get the idea.

  </p>
    (Originally
    <a href="http://web.archive.org/web/20161006154511/http://www.lshift.net/blog/2013/07/31/expanding-reducers/">
http://web.archive.org/web/20161006154511/http://www.lshift.net/blog/2013/07/31/expanding-reducers/</a>)

    </body></html>